tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}

apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            keyAlias 'freedcamkey'
            keyPassword 'freedcam'
            storeFile file('./freedcamkey.jks')
            storePassword 'freedcam'
        }
        debug {
            storeFile file('./freedcamkey.jks')
            storePassword 'freedcam'
            keyAlias = 'freedcamkey'
            keyPassword 'freedcam'
        }
    }

    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    defaultConfig {
        // FIXME: 2021/6/28 增加尺寸设置用于保存 CameraApi2 JPEG 图片为 HEIF 格式
        minSdkVersion 28
        targetSdkVersion 30
        versionCode 239
        versionName '4.3.39'
        applicationId "troop.com.freedcamx"
        renderscriptTargetApi 24
        setProperty("archivesBaseName", "FreeDcam_" + name + "_" + versionName)
        externalNativeBuild {
            ndkBuild {
                arguments "-j4"
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
        signingConfig signingConfigs.config
    }
    lintOptions { checkReleaseBuilds false }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            ndk.debugSymbolLevel 'full'
        }
        debug {
            jniDebuggable true
            signingConfig signingConfigs.config
        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
    productFlavors {}

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        dataBinding true
    }
}

dependencies {
    implementation project(':library')

    // 加密
    implementation 'com.facebook.conceal:conceal:2.0.2@aar'
    annotationProcessor 'androidx.annotation:annotation:1.2.0'

    // 日志库 https://github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // 状态栏颜色 https://github.com/jgilfelt/SystemBarTint
    implementation 'com.readystatesoftware.systembartint:systembartint:1.0.3'

    implementation 'pub.devrel:easypermissions:3.0.0'
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:3.2.0"

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.github.ChickenHook:RestrictionBypass:2.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:1.10.19'
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "FreeDcam_${variant.buildType.name}_${variant.versionName}.apk"
    }
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/central' }
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url 'https://maven.aliyun.com/repository/google' }
    maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }

    google()
    jcenter()
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
    maven { url 'https://dl.bintray.com/jenly/maven' }
    maven { url "https://repo.eclipse.org/content/repositories/paho-releases/" }

    flatDir {
        dirs 'libs'
    }
}

